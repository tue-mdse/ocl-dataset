MainModel : /Model/SimplePDL.ecore


context Process
inv nameIsDefined:
    if  self.name.oclIsUndefined() then
	false
    else
	self.name <> ''
    endif

inv containmentConsistency:
  let elements : Set(ProcessElement) = self.processElements in
    let wds: Set(ProcessElement) = elements->select( e | e.oclIsTypeOf(WorkDefinition)) in
      let wss: Set(ProcessElement) = elements->select( e | e.oclIsTypeOf(WorkSequence)) in
        -- WS linked to a process WD are elements of this process
        wds->forAll(wd: ProcessElement |
            elements->includesAll(wd.oclAsType(WorkDefinition).linksToPredecessors)
            and elements->includesAll(wd.oclAsType(WorkDefinition).linksToSuccessors))
        -- source and target of a process WS are elements of this process
        and wss->forAll(ws: ProcessElement |
            elements->includes(ws.oclAsType(WorkSequence).predecessor)
            and elements->includes(ws.oclAsType(WorkSequence).successor))

inv WDparameterConsistency:
  let elements : Set(ProcessElement) = self.processElements in
    let wds: Set(ProcessElement) = elements->select( e | e.oclIsTypeOf(WorkDefinition)) in
      let pars: Set(ProcessElement) = elements->select( e | e.oclIsTypeOf(Parameter)) in
        -- ParWD linked to a process WD are elements of this process
        wds->forAll(wd: ProcessElement |
			elements->includesAll(wd.oclAsType(WorkDefinition).needs))
        and pars->forAll(par: ProcessElement |
            elements->includes(par.oclAsType(Parameter).workDefinition))

inv parameterResourceConsistency:
  let elements : Set(ProcessElement) = self.processElements in
    let pars: Set(ProcessElement) = elements->select( e | e.oclIsTypeOf(Parameter)) in
      let res: Set(Resource) = self.resources in
        -- ParWD linked to a resource Res are elements of this process
        pars->forAll(par: ProcessElement |
			res->includes(par.oclAsType(Parameter).resource))

inv processElementsDiffName :
	self.processElements->forAll(e1,e2 | e1<>e2 implies e1.oclAsType(WorkDefinition).name<>e2.oclAsType(WorkDefinition).name)

inv parameterDiffName :
	self.processElements->forAll(e1,e2 | e1<>e2 implies e1.oclAsType(Parameter).name<>e2.oclAsType(Parameter).name)

inv nameSize :
	self->forAll(e|e.name.size()>0)

inv min_timeMax_timeAreDefined:
    if  self.min_time.oclIsUndefined() or self.max_time.oclIsUndefined() then
	false
    else
	self.min_time >= 0 and (self.max_time >= self.min_time or self.max_time = -1)
    endif

inv resourcesDiffName:
	self.resources->forAll(r1,r2 | r1<>r2 implies r1.oclAsType(Resource).name<>r2.oclAsType(Resource).name)

inv globalMax_time:
	self.processElements->forAll(e | (e.oclAsType(WorkDefinition).max_time <= self.max_time or self.max_time = -1))

context WorkSequence
inv notReflexive:
	self.predecessor<>self.successor

context WorkDefinition
inv nameIsDefined:
    if  self.name.oclIsUndefined() then
	false
    else
	self.name <> ''
    endif

inv min_timeMax_timeAreDefined:
    if  self.min_time.oclIsUndefined() or self.max_time.oclIsUndefined() then
	false
    else
	self.min_time >= 0 and (self.max_time >= self.min_time or self.max_time = -1)
    endif

inv parameterDiffResource:
	self.needs->forAll(p1,p2 | p1<>p2 implies p1.resource.oclAsType(Resource).name<>p2.resource.oclAsType(Resource).name)

context Resource
inv nameIsDefined:
    if  self.name.oclIsUndefined() then
	false
    else
	self.name <> ''
    endif

inv markingIsDefined:
    if  self.marking.oclIsUndefined() then
	false
    else
	self.marking >= 0
    endif

context Parameter
inv nameIsDefined:
    if  self.name.oclIsUndefined() then
	false
    else
	self.name <> '' and self.name = self.resource.oclAsType(Resource).name
    endif

inv nbNeedsIsDefined:
    if  self.nbNeeds.oclIsUndefined() then
	false
    else
	self.nbNeeds >= 0 and self.nbNeeds <= self.resource.oclAsType(Resource).marking
    endif